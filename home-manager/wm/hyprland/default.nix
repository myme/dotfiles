{
  config,
  lib,
  pkgs,
  specialArgs,
  ...
}:

let
  cfg = config.myme.wm.hyprland;
  wallpaper = "${pkgs.myme.wallpapers}/nebula-abstract.jpg";
  hyprquit = pkgs.writeShellScriptBin "hyprquit" ''
    #!${pkgs.bash}/bin/bash
    answer="$(rofi -dmenu -p "Really quit?" <<< $'No\nYes')"
    [ "$answer" = "Yes" ] && loginctl terminate-user $USER
  '';
  withUWSM = specialArgs.nixosConfig.programs.hyprland.withUWSM;

in
{
  options.myme.wm.hyprland = {
    enable = lib.mkEnableOption "Hyprland - Tiling compositor with the looks";
  };

  config = lib.mkIf cfg.enable {
    home.packages = [
      hyprquit
      pkgs.alsa-utils # for volume control
      pkgs.myme.pkgs.hyprgrab
      pkgs.nwg-displays
      pkgs.wl-clipboard
      pkgs.wlr-randr
    ];

    # cursor ðŸ–±
    home.pointerCursor.hyprcursor.enable = true;

    # waybar status bar ðŸ«
    myme.wm.waybar.enable = true;

    # protect me precious lucky charms ðŸ€
    programs.hyprlock = {
      enable = true;
      settings = {
        # sample hyprlock.conf
        # for more configuration options, refer
        # https://wiki.hyprland.org/Hypr-Ecosystem/hyprlock
        general = {
          hide_cursor = true;
        };
        input-field = {
          # monitor = ;
          fade_on_empty = true;
          font_color = "rgba(255, 121, 198, 0.5)";
          inner_color = "rgba(0, 0, 0, 0.5)";
        };
        background = {
          # path = wallpaper;
          path = "screenshot";
          color = "rgb(23, 39, 41)";
          blur_passes = 2;
        };
      };
    };

    # idle handling ðŸ’¤
    services.hypridle = {
      enable = true;
      settings = {
        general = {
          before_sleep_cmd = "loginctl lock-session";
          after_sleep_cmd = "hyprctl dispatch dpms on";
          ignore_dbus_inhibit = false;
          lock_cmd = "hyprlock";
        };

        listener = [
          {
            timeout = 300;
            on-timeout = "hyprlock";
          }
          {
            timeout = 1200;
            on-timeout = "hyprctl dispatch dpms off";
            on-resume = "hyprctl dispatch dpms on";
          }
        ];
      };
    };

    # wallpapers ðŸ–¼
    services.hyprpaper = {
      enable = true;
      settings = {
        ipc = "on";
        # splash = false;
        # splash_offset = 2.0;
        preload = [ wallpaper ];
        wallpaper = [
          ",${wallpaper}"
          # "DP-1,/share/wallpapers/cat_pacman.png"
        ];
      };
    };

    # autoname workspaces ðŸ¤–
    systemd.user.services.hyprland-autoname-workspaces = {
      Install = {
        WantedBy = [ config.wayland.systemd.target ];
      };

      Unit = {
        ConditionEnvironment = "WAYLAND_DISPLAY";
        Description = "hyprland-autoname-workspaces";
        After = [ config.wayland.systemd.target ];
        PartOf = [ config.wayland.systemd.target ];
      };

      Service = {
        ExecStart = "${pkgs.hyprland-autoname-workspaces}/bin/hyprland-autoname-workspaces --config ${./hyprland-autoname-workspaces.toml}";
        Restart = "always";
        RestartSec = "10";
      };
    };

    # my eyes! ðŸŒ„
    # TODO: Switch to hyprsunset once it supports automatic transitions
    # See: https://github.com/hyprwm/hyprsunset/issues/8
    systemd.user.services.wlsunset = {
      Install = {
        WantedBy = [ config.wayland.systemd.target ];
      };

      Unit = {
        ConditionEnvironment = "WAYLAND_DISPLAY";
        Description = "wlsunset";
        After = [ config.wayland.systemd.target ];
        PartOf = [ config.wayland.systemd.target ];
      };

      Service = {
        ExecStart = "${pkgs.wlsunset}/bin/wlsunset -l 59.777839 -L 10.801630";
        Restart = "always";
        RestartSec = "10";
      };
    };

    # main config
    wayland.windowManager.hyprland = {
      enable = true;
      xwayland.enable = true;
      systemd = {
        enable = !withUWSM;
        variables = [ "--all" ];
      };
      extraConfig = builtins.readFile ./hyprland.conf;
    };

    # Env variables for Hyprland session
    xdg.configFile."uwsm/env".source =
      lib.mkIf withUWSM "${config.home.sessionVariablesPackage}/etc/profile.d/hm-session-vars.sh";
  };
}
